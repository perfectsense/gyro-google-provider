/*
 * Copyright 2019, Perfect Sense, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'maven-publish'
}

def releaseVersion = '0.99.3'
def releaseBuild = false;

if (System.getenv('TRAVIS_BRANCH') && System.getenv('TRAVIS_PULL_REQUEST') == 'false') {
    defaultTasks 'build', 'publish'

    releaseBuild = System.getenv('TRAVIS_BRANCH').startsWith("release/");
} else {
    defaultTasks 'build', 'publishToMavenLocal'
}

group = 'gyro'
version = releaseBuild ? releaseVersion : releaseVersion + '-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()
    google()

    maven {
        url 'https://artifactory.psdops.com/public'
    }

    maven {
        url 'https://artifactory.psdops.com/gyro-snapshots'
    }

    maven {
        url 'https://artifactory.psdops.com/gyro-releases'
    }
}

configurations {
    gyroDoclet
}

dependencies {
    api 'gyro:gyro-core:1.0.0' + (releaseBuild ? '' : '-SNAPSHOT')

    implementation enforcedPlatform('com.google.cloud:libraries-bom:20.4.0')
    // XXX: com.google.apis:google-api-services-iam:v1-rev316-1.25.0 uses older version of com.google.api-client:google-api-client
    implementation 'com.google.api-client:google-api-client:1.31.5'
    implementation 'com.google.apis:google-api-services-iam:v1-rev316-1.25.0'
    implementation 'com.google.cloud:google-cloud-compute'
    implementation 'com.google.cloud:google-cloud-dns'
    implementation 'com.google.cloud:google-cloud-kms'
    implementation 'com.google.cloud:google-cloud-resourcemanager'
    implementation 'com.google.cloud:google-cloud-storage'
    implementation 'com.google.cloud:google-cloud-pubsub'
    implementation 'com.google.cloud:google-cloud-artifact-registry:0.2.1'
    implementation 'com.psddev:dari-util:3.3.607-xe0f27a'

    gyroDoclet "gyro:gyro-doclet:1.0.0"
}

checkstyle {
    toolVersion = '8.24'
    configFile = rootProject.file('config/checkstyle.xml')
}

task referenceDocs(type: Javadoc) {
    title = null // Prevents -doctitle and -windowtitle from being passed to GyroDoclet
    source = sourceSets.main.allJava
    classpath = configurations.runtimeClasspath
    options.doclet = "gyro.doclet.GyroDoclet"
    options.docletpath = configurations.gyroDoclet.files.asType(List)
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('GRADLE_DEPLOY_USER')
                password System.getenv('GRADLE_DEPLOY_PASSWORD')
            }

            if (releaseBuild) {
                url 'https://artifactory.psdops.com/gyro-releases'

            } else {
                url 'https://artifactory.psdops.com/gyro-snapshots'
            }
        }
    }
}
